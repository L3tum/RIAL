?start : [(struct_decl|global_variable_decl|attributed_func_decl|function_decl|using|attributed_compiler_directive)*]
?statement : conditional_block
           | conditional_elif_block
           | switch_block
           | for_loop
           | while_loop
           | loop_loop
           | function_call
           | expression ";"
           | variable_decl ";"
           | using ";"
           | continue_rule
           | break_rule
           | return_rule
           | attribute


// Control flow structures
conditional_block : "if" "(" expression ")" "{" [statement*] "}"
                  | "if" "(" expression ")" "{" [statement*] "}" "else" "{" [statement*] "}"
conditional_elif_block : "if" "(" expression ")" "{" [statement*] "}" ("elif" "(" expression ")" "{" [statement*] "}")+
                       | "if" "(" expression ")" "{" [statement*] "}" ("elif" "(" expression ")" "{" [statement*] "}")+ "else" "{" [statement*] "}"
switch_block : "switch" "(" expression ")" "{" (switch_case|default_case)+ "}"
switch_case : "case" expression ":" ("{" [statement*] "}")?
default_case : "default" ":" ("{" [statement*] "}")?
for_loop : "for" "(" variable_decl ";" expression ";" expression ")" "{" [statement*] "}"
while_loop : "while" "(" expression ")" "{" [statement*] "}"
loop_loop : "loop" "{" [statement*] "}"
continue_rule : "continue" ";"
break_rule : "break" ";"
return_rule : "return" ";"
            | "return" expression ";"

// Shorthand structures
shorthand_if : "if" expression "?" expression ":" expression

// Variables
global_variable_decl : variable_decl ";"
variable_decl : "var" IDENTIFIER ASSIGN expression
variable_assignment : var ASSIGN expression

// Math shortcuts
?variable_arithmetic : variable_increment
                     | variable_decrement
                     | variable_multiplication
                     | variable_division
variable_increment : IDENTIFIER "++"
                   | IDENTIFIER "+=" expression
variable_decrement : IDENTIFIER "--"
                   | IDENTIFIER "-=" expression
variable_multiplication : IDENTIFIER "*=" expression
variable_division : IDENTIFIER "/=" expression

// Structs
struct_decl : ACCESS_MODIFIER? "struct" IDENTIFIER [":" IDENTIFIER ("," IDENTIFIER)*] "{" [(attributed_func_decl|function_decl|struct_property_declaration ";")*] "}"
struct_property_declaration : ACCESS_MODIFIER? IDENTIFIER IDENTIFIER ["=" expression]

// Functions
function_call : IDENTIFIER ("." IDENTIFIER)* "(" [expression ("," expression)*] ")"
               | "new" IDENTIFIER ("." IDENTIFIER)* "(" [expression ("," expression)*] ")"
function_decl : ACCESS_MODIFIER? IDENTIFIER IDENTIFIER "(" [PARAMS? IDENTIFIER IDENTIFIER ("," IDENTIFIER IDENTIFIER | "," PARAMS IDENTIFIER IDENTIFIER)*] ")" "{" [statement*] "}"
               | EXTERNAL ACCESS_MODIFIER? IDENTIFIER IDENTIFIER "(" [PARAMS? IDENTIFIER IDENTIFIER ("," IDENTIFIER IDENTIFIER | "," PARAMS IDENTIFIER IDENTIFIER)*] ")" ";"

// Attributes
?attribute : "[" function_call "]"
attributed_func_decl : attribute+ function_decl
attributed_compiler_directive : PREPRO_FLAG attribute

// Base expression
?expression : function_call
            | expression PLUS expression -> addition
            | expression MINUS expression -> subtraction
            | expression MUL expression -> multiplication
            | expression DIV expression -> division
            | expression SMALLER expression -> smaller_than
            | expression BIGGER expression -> bigger_than
            | expression BIGGER_EQUAL expression -> bigger_equal
            | expression SMALLER_EQUAL expression -> smaller_equal
            | expression EQUAL expression -> equal
            | STRING -> string
            | "true" -> true
            | "false" -> false
            | "null" -> null
            | NUMBER -> number
            | var
            | variable_assignment
            | variable_arithmetic
            | shorthand_if
            | "(" expression ")"

var : IDENTIFIER ("." IDENTIFIER)*


using : "use" IDENTIFIER (":" IDENTIFIER)* ";"
ACCESS_MODIFIER.2 : "private" | "public" | "internal"

DIV.-4 : "/"
MUL.-4 : "*"
MINUS.-5 : "-"
PLUS.-5 : "+"
BIGGER.-6 : ">"
SMALLER.-6 : "<"
BIGGER_EQUAL.-7 : ">="
SMALLER_EQUAL.-7 : "<="
EQUAL.-7 : "=="
ASSIGN.-8 : "="

NUMBER.-3 : (PLUS|MINUS)? (FLOAT_SC|FLOAT|INT|HEX_INT|BIN_INT) ("uL"|"U"|"Ul"|"UL"|"ul"|"l"|"u"|"L"|"b"|"B")?
FLOAT_SC.-1 : (FLOAT|INT) "e" ("-"|"+")? INT
FLOAT.-2 : (INT+ "." INT+|INT+ "."|"." INT+) ("f"|"d"|"h")?
BIN_INT : "0b" ("0"|"1"|"_")+
HEX_INT : "0x" HEXDIGIT (HEXDIGIT|"_")*
INT : DIGIT (DIGIT|"_")*
EXTERNAL : "external"
PARAMS: "params"
PREPRO_FLAG : "#"

// Taken from common definition, copied to assign -10 priority
IDENTIFIER.-10 : /(?!(^(var|return|use|external|params|public)$))#?(_|[a-zA-Z])(_|[a-zA-Z0-9])*/

COMMENT : "//" /[^\n]*/ "\n"
ML_COMMENT : "/*" /[^*]*/ "*/"

%import common.ESCAPED_STRING -> STRING
%import common.LETTER
%import common.DIGIT
%import common.HEXDIGIT
%import common.WS
%ignore WS
%ignore COMMENT
%ignore ML_COMMENT